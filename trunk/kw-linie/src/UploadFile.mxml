<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" title="Upload File">
	
	<mx:Script>
		<![CDATA[
			import mx.managers.PopUpManager;
			import mx.controls.Alert;
			
			private var fileReference : FileReference = new FileReference();
			private var request:URLRequest;
			private var popupParent: DisplayObject;
			public var requestUrl: String = "upload_file.php";
			
			private function removeMe():void {
                PopUpManager.removePopUp(this);
            }
            
            private function cancel_click() : void {
            	fileReference.cancel();
            	removeMe();
            }

			public function onUploadProgress(event : ProgressEvent) : void {
				progressBar.setProgress(event.bytesLoaded, event.bytesTotal);
			}
		
			public function onUploadComplete(event : Event) : void {
				removeMe();
				Alert.show("Upload complete");
			}
		
			public function onSelect(event : Event) : void {
				PopUpManager.addPopUp(this, popupParent, true);

	            var fileReference: FileReference = FileReference(event.target);
	            
				request = new URLRequest();
                request.url = requestUrl;
                request.method = URLRequestMethod.POST;

            	fileReference.upload(request, "file");
			}
		
			public function onUploadIoError(event : IOErrorEvent) : void {
				removeMe();
				Alert.show(event.text);
			}
		
			public function onUploadSecurityError(event : SecurityErrorEvent) : void {
				removeMe();
				Alert.show(event.text);
			}
		
			public function upload(popupParent: DisplayObject) : void {
				this.popupParent = popupParent;

				fileReference = new FileReference();

				fileReference.addEventListener(Event.SELECT, onSelect);
                fileReference.addEventListener(ProgressEvent.PROGRESS, onUploadProgress);
                fileReference.addEventListener(Event.COMPLETE, onUploadComplete);
                fileReference.addEventListener(IOErrorEvent.IO_ERROR, onUploadIoError);
                fileReference.addEventListener(SecurityErrorEvent.SECURITY_ERROR, onUploadSecurityError);
                
                fileReference.browse();
			}
		]]>
	</mx:Script>

	<mx:VBox paddingBottom="10" paddingLeft="10" paddingRight="10" paddingTop="10">
		<mx:ProgressBar labelPlacement="left" id="progressBar" mode="manual"/>
	</mx:VBox>

	<mx:ControlBar horizontalAlign="center">
		<mx:Button label="Cancel" click="cancel_click()"/>
	</mx:ControlBar>

</mx:TitleWindow>
